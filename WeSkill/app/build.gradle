plugins {
    id 'com.android.application'
//    id 'com.google.gms.google-services'
//    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
/** There are supported type:
 * dev - code 0
 * alpha - code 1
 * beta - code 2
 * no suffix - code 9 - means final production build from market.*/
def versionType = 0
def versionTypeName = null

android {
    compileSdk 30

    defaultConfig {
        applicationId "in.co.weskill.sample"
        minSdk 21
        targetSdk 30
        versionCode(versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionType)
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        switch (versionType) {
            case 0:
                versionTypeName = "dev"
                break
            case 1:
                versionTypeName = "alpha"
                break
            case 2:
                versionTypeName = "beta"
                break
            default:
                versionTypeName = "final"
                break
        }
        archivesBaseName = "WeSkill_" + versionName + "_" + versionTypeName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        release {
//            try {
//                keyAlias KEY_ALIAS
//                keyPassword KEY_PASSWORD
//                storeFile file(KEYSTORE_PATH)
//                storePassword KEYSTORE_PASSWORD
//            } catch (e) {
//                throw new InvalidUserDataException("You must check all the variable in the gradle.properties file.")
//            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
            signingConfig signingConfigs.debug
            resValue "string", "name_app", "WeSkill_debug"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"
        }
        releaseDebuggable {
            initWith release
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    // Base
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    // Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // region Firebase

    implementation 'com.google.firebase:firebase-crashlytics:18.2.1'
    implementation 'com.google.firebase:firebase-analytics:19.0.1'

    // endregion

    implementation 'com.jakewharton.timber:timber:4.7.1'

}