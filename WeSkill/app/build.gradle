plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
/** There are supported type:
 * dev - code 0
 * alpha - code 1
 * beta - code 2
 * no suffix - code 9 - means final production build from market.*/
def versionType = 0
def versionTypeName = null

android {
    buildFeatures {
        viewBinding true
    }
    compileSdk 30

    defaultConfig {
        applicationId "in.co.weskill.sample"
        minSdk 21
        targetSdk 30
        versionCode(versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionType)
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        switch (versionType) {
            case 0:
                versionTypeName = "dev"
                break
            case 1:
                versionTypeName = "alpha"
                break
            case 2:
                versionTypeName = "beta"
                break
            default:
                versionTypeName = "final"
                break
        }
        archivesBaseName = "WeSkill_" + versionName + "_" + versionTypeName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        release {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(KEYSTORE_PATH)
                storePassword KEYSTORE_PASSWORD
            } catch (e) {
                throw new InvalidUserDataException("You must check all the variable in the gradle.properties file.")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
            signingConfig signingConfigs.debug
            resValue "string", "name_app", "WeSkill_debug"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
            resValue "string", "client_id", "439711863661-va82bvrs9kf3oi8jff41j7s2ic61esnv.apps.googleusercontent.com"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "name_app", "WeSkill"
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"
            resValue "string", "client_id", "439711863661-va82bvrs9kf3oi8jff41j7s2ic61esnv.apps.googleusercontent.com"
        }
        releaseDebuggable {
            initWith release
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    // Base
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation("androidx.fragment:fragment-ktx:1.3.6")
//    implementation "androidx.recyclerview:recyclerview:1.2.1"
//    implementation 'com.github.bumptech.glide:glide:4.11.0'


    // Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // region Firebase

    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // endregion

    // player
    implementation "com.google.android.exoplayer:exoplayer:2.15.0"

    implementation 'com.jakewharton.timber:timber:4.7.1'

}